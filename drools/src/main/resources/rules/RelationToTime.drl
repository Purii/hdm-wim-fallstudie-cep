//created on: 10.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.ComplexTokenEvent
import de.hdm.wim.Event
import java.time.LocalTime;

//declare any global variables here

declare Event
	@role(event)
end
declare ComplexTokenEvent
	@role(event)
end

declare window CurrentEvent
	Event() over window:length(1)
end

declare window LastEvents
	// Aktuelle wird mitgezaehlt
	Event() over window:length(2)
end

declare window LastComplexTokenEvent
	ComplexTokenEvent() over window:length(1)
end

// Die Regel greift unter folgenden Bedingungen:
// Wenn zunächst ein Event mit dem Token "at" erkannt wird
// und im direkten Anschluss ein Event fällt, welches einen Token gemäß des definierten regulären Ausdrucks enthält
// Anschließend wird das Attribut "relatedToTime" des ComplexTokenEvents gesetzt
rule "Identify relation to time"
	no-loop
	when
		$prevToken : Event(
			token == "at"
		) from window LastEvents
		Event(
			token matches "([0-9]|0[0-9]|1[0-9]|2[0-3]):[0-5][0-9]",
			$time : token,
			// der aktuelle Token muss direkt auf den Vorgänger "at" folgen
			this after $prevToken
		)
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		modify($complexTokenEvent) {
			setRelatedToTime(LocalTime.parse( $time ))
		}
end

rule "Translate relation to time [0-12]pm/am"
	no-loop
	when
		$timeEvent : Event(
			token matches "((0?[1-9])|1[0-2])(am|pm)",
			$time : token
		) from window CurrentEvent
	then
		String $time24h = new String($time);
		if($time24h.endsWith("pm")) {
			$time24h = String.valueOf(Integer.parseInt($time24h.replace("pm", "")) + 12);
		} else if($time24h.endsWith("am")) {
			$time24h = $time24h.replace("am", "");
		}
		// Trigger "Identify relation to time"
		modify($timeEvent) {
			setToken($time24h + ":00");
		}
end

rule "Merge time format 1 am/pm to 1am/pm"
	when
		$prevToken : Event(
			token matches "(0?[1-9])|1[0-2]",
			$time : token
		) from window LastEvents
		$nextToken : Event(
			token == "am"
			|| token == "pm",
			$ampm : token,
			this after $prevToken
		)
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		// Ausblick zweites Semester:
		// Does retract work? Won't trigger "relation to time" due missing previous token == "at"
		retract($prevToken);
		// modify($nextToken) {
		//	setToken($time + $ampm);
		// }
		// Workaround
		modify($complexTokenEvent) {
			setRelatedToTime(LocalTime.parse( $time + ":00" ))
		}
end
