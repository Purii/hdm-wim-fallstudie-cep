//created on: 10.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.ComplexTokenEvent
import de.hdm.wim.Event

//declare any global variables here
declare Event
	@role(event)
end

declare ComplexTokenEvent
	@role(event)
end

declare window CurrentEvent
	Event() over window:length(1)
end

declare window LastEvents
	// Aktuelle wird mitgezaehlt
	Event() over window:length(2)
end

declare window LastComplexTokenEvent
	ComplexTokenEvent() over window:length(1)
end

rule "Identify Milestone"
	no-loop
	when
		Event(
			token == "milestone"
			|| token == ComplexTokenEvent.MILESTONE
		) from window CurrentEvent
		// Only most recent ComplexTokenEvent
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		modify($complexTokenEvent) {
			addTopic(ComplexTokenEvent.MILESTONE)
		}
end

rule "Identify Tasklist"
	no-loop
	when
		Event(
			token == "tasks"
			|| token == ComplexTokenEvent.TASKLIST
		) from window CurrentEvent
		// Only most recent ComplexTokenEvent
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		modify($complexTokenEvent) {
			addTopic(ComplexTokenEvent.TASKLIST)
		}
end

// Durch Aggregation kann aus zwei Events ein einzelnes Event mit vereinheitlichtem Vokabular erstellt werden
rule "Merge separated Tasklist"
	when
		$prevToken : Event(
			token == "task"
		) from window LastEvents
		$nextToken : Event(
			token == "list",
			this after $prevToken
		)
	then
		retract($prevToken);
		modify($nextToken) {
			setToken(ComplexTokenEvent.TASKLIST);
		}
end
