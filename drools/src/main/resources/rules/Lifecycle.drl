//created on: 09.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.LifecycleEvent
import de.hdm.wim.ComplexTokenEvent


//declare any global variables here
declare ComplexTokenEvent
	@role(event)
end

declare window LastComplexTokenEvent
	ComplexTokenEvent() over window:length(1)
end

// Lifecycle wird gestartet:
// Neues ComplexTokenEvent anlegen, ohne Vorbelegung m√∂glicher Attribute
rule "Start Lifecycle of ComplexTokenEvent"
	// Modify will cause infinite loop
	lock-on-active
	when
		LifecycleEvent(token == "start") and
		not ($ComplexTokenEvent : ComplexTokenEvent(
				relatedToProject != null
			) from window LastComplexTokenEvent
		)
	then
		insert(new ComplexTokenEvent());
end

// Lifecycle wird gestartet / Alternative "mit Vorbelegung":
// ComplexTokenEvent anlegen, mit Vorbelegung des Attributes "relatedToProject"
// Regel greift nur, wenn unmittelbar davor bereits ein ComplexTokenEvent mit entsprechenden Attribut existiert
rule "Start Lifecycle of ComplexTokenEvent when there is already a project"
	// Modify will cause infinite loop
	lock-on-active
	when
		(LifecycleEvent(token == "start") and
		$ComplexTokenEvent : ComplexTokenEvent(
				relatedToProject != null
			) from window LastComplexTokenEvent
		)
	then
		ComplexTokenEvent $nextComplexTokenEvent = new ComplexTokenEvent();
		$nextComplexTokenEvent.setRelatedToProject($ComplexTokenEvent.getRelatedToProject());
		insert($nextComplexTokenEvent);
end

// Lifecycle wird beendet:
// ComplexTokenEvent mit allen Attributen wird ausgegeben
rule "End Lifecycle of ComplexTokenEvent"
	// Modify will cause infinite loop
	lock-on-active
	when
		LifecycleEvent(token == "end")
		// Only most recent ComplexTokenEvent
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		//System.out.println($complexTokenEvent);
		System.out.println($complexTokenEvent.toJson());
end
