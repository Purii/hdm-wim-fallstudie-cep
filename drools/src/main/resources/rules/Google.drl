//created on: 10.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.ComplexTokenEvent
import de.hdm.wim.Event


//declare any global variables here
declare Event
	@role(event)
end
declare ComplexTokenEvent
	@role(event)
end

declare window CurrentEvent
	Event() over window:length(1)
end

declare window LastEvents
	// Aktuelle wird mitgezaehlt
	Event() over window:length(2)
end

declare window LastComplexTokenEvent
	ComplexTokenEvent() over window:length(1)
end


rule "Calendar"
	no-loop
	when
		Event(
			token == "appointment"
			|| token == "meeting"
		) from window CurrentEvent
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		modify($complexTokenEvent) {
			addTopic(ComplexTokenEvent.CALENDAR);
		}
end


rule "Identify Google Drive"
	no-loop
	when
		Event(
			token == "drive"
			|| token == "Google drive"
			|| token == ComplexTokenEvent.DRIVE
		) from window CurrentEvent
		// Only most recent ComplexTokenEvent
		$complexTokenEvent : ComplexTokenEvent() from window LastComplexTokenEvent
	then
		modify($complexTokenEvent) {
			addTopic(ComplexTokenEvent.DRIVE)
		}
end

rule "Merge separated Google Drive"
	when
		$prevToken : Event(
			token == "google"
		) from window LastEvents
		$nextToken : Event(
			token == "drive",
			this after $prevToken
		)
	then
		retract($prevToken);
		modify($nextToken) {
			setToken(ComplexTokenEvent.DRIVE);
		}
end
