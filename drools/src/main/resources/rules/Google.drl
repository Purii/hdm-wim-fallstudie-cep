//created on: 10.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.ComplexToken
import de.hdm.wim.Event


//declare any global variables here
declare Event
	@role(event)
end
declare ComplexToken
	@role(event)
end

declare window CurrentEvent
	Event() over window:length(1)
end

declare window LastEvents
	// Aktuelle wird mitgezaehlt
    Event() over window:length(2)
end

declare window LastComplexTokenEvent
    ComplexToken() over window:length(1)
end


rule "Calendar"
    lock-on-active true
    when
        Event(
       		token == "appointment"
        	|| token == "meeting"
        )
        $complexToken : ComplexToken() from window LastComplexTokenEvent
    then
        modify($complexToken) {
    		addTopic(ComplexToken.CALENDAR);
    	}
end


rule "Identify Google Drive"
	no-loop
    when
        Event(
       		token == "drive"
       		|| token == "googledrive"
       		|| token == "Google drive"
        ) from window CurrentEvent
        // Only most recent ComplexToken
       	$complexToken : ComplexToken() from window LastComplexTokenEvent
    then
    	modify($complexToken) {
    		addTopic(ComplexToken.DRIVE)
    	}
end

rule "Translate separated Google Drive"
    when
        $prevToken : Event(
        	token == "google"
        ) from window LastEvents
        $nextToken : Event(
       		token == "drive",
       		this after $prevToken
        )
    then
    	retract($prevToken);
    	modify($nextToken) {
    		setToken(ComplexToken.DRIVE);
    	}
end
