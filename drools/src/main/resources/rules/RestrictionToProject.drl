//created on: 09.01.2017
package de.hdm.wim

//list any import classes here.
import de.hdm.wim.ComplexToken
import de.hdm.wim.Event

//declare any global variables here
declare Event
	@role(event)
end
declare ComplexToken
	@role(event)
end

declare window LastEvents
	// Aktuelle wird mitgezaehlt
    Event() over window:length(2)
end

declare window LastComplexTokenEvent
    ComplexToken() over window:length(1)
end

rule "Identify 'leading to'"
	when
		$prevToken : Event(
			token == "leading"
		) from window LastEvents
		$postfix : Event (
			token == "to",
			this after $prevToken
		)
	then
		modify($postfix) {
			setToken("concerning");
		}
end

rule "Recognize project based restriction"
    // Modify will cause infinite loop
	lock-on-active true
    when
       	$expectRestriction : Event(
       		token == "concerning"
        	|| token == "regarding"
       	) from window LastEvents
       	$restriction : Event(
       		this after $expectRestriction
       	)
       	// Only most recent ComplexToken
       	$complexToken : ComplexToken() from window LastComplexTokenEvent
    then
    	modify($complexToken) {
    		setRestrictedToProject($restriction.getToken())
    	}
end